buildscript {
    ext {
        springBootVersion = '1.4.1.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jooq:jooq-codegen:3.8.4")
        classpath("org.jooq:jooq-meta:3.8.4")
        classpath("org.jooq:jooq:3.8.4")
        classpath("org.postgresql:postgresql:9.4.1211.jre7")
        classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2-rc2")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'org.sonarqube'
apply plugin: 'jacoco'

jar {
    baseName = 'demo'
    version = '1.0'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}
task generate << {
    def writer = new StringWriter()
    def xml = new groovy.xml.MarkupBuilder(writer)
            .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.8.0.xsd') {
        jdbc() {
            driver('org.postgresql.Driver')
            url('jdbc:postgresql://localhost:5432/demodb')
            user('postgres')
            password('admin')
        }
        generator() {
            database() {
                name('org.jooq.util.postgres.PostgresDatabase')
                inputSchema('public')
            }
            generate() {
                pojos('true')
                instanceFields('true')
                deprecated('false')
            }
            target() {
                packageName('jooq.codgen')
                directory('src/main/java')
            }
        }
    }

    org.jooq.util.GenerationTool.generate(
            javax.xml.bind.JAXB.unmarshal(new StringReader(writer.toString()), org.jooq.util.jaxb.Configuration.class)
    )
}

jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
}

sonarqube {
    properties {
        property "sonar.projectName", "Instructure-Demo-Boot-Coverage"
        property "sonar.projectKey", "Instructure-Demo-Boots"
        property "sonar.projectVersion", "1.0"
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/jacoco-it.exec"
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-jooq')
    compile('org.jooq:jooq-meta')
    compile('org.jooq:jooq-codegen')
    compile('org.modelmapper.extensions:modelmapper-jooq:0.7.7')
    runtime('org.postgresql:postgresql')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}